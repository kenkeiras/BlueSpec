(ql:quickload "cl-html5-parser")
(ql:quickload "cl-fad")
(ql:quickload "cl-ppcre")


(defpackage #:bluespec
  (:use :cl :html5-parser :cl-fad :cl-ppcre)
  (:export #:spec-page #:print-docs #:read-docs #:reload-docs))

(in-package #:bluespec)


(defparameter *UNDERLINES* "=-;:_.,\"'")

(defclass spec-page()
  ((title :accessor title)
   (rst :accessor rst)
   (raw :accessor raw)
   (toctreep :accessor toctreep)
   (link-table :accessor link-table)))




(defmethod initialize-instance :after ((page spec-page)
                                       &key ((:content content)))
  (setf (raw page) content)
  (setf (link-table page) (make-hash-table :test #'equalp))

  (let ((title (get-text (car (get-in-toplevel content "h2")))))
    (setf (title page)
          (strip (first
                  (flatten
                   (list
                    (if title title
                        (subseq
                         (caddr (car (get-in-toplevel content "title")))
                         6))))))))

  (let ((restructured (flatten (xmls-to-rst (get-content content)
                                            (read-title-index (title page))
                                            page))))
    (setf (toctreep page) (every (lambda (X) (or (includep X)
                                                 (= (length X) 0)))
                                 (mapcar #'strip restructured)))
    (setf (rst page) (format NIL "狺ア蝈篝蝓泗躜邃┅Ж麒孱戾铉翳蝈徜糸綮瀛轭溴糸綮疳珏┅博ㄦ矧磲狺ア糸綮疳珏┅┅ㄤ彐躅珏舡轭麸痨弼屐ㄣ镱翦铘翎绌祜镳骘孱趄轭泔铘孱殒ㄡ钿扉篝孱趄篝蜷铉ㄣ狎孱趄翎绌泔祆邈孱趄┅ㄤ彐躅珏舡翦翎绌ㄩ扉篝翎绌戾è屐屙孱趔ㄣ滗翎绌┅祜镳骘屐屙孱轭屐屙孱趔泔祆邈ㄧ弭翦屐屙孱舂┅翎绌ㄤ彐躅骒狒翦飑ㄣ镱è铛祆飑紊泰è狒镯飑扉篝飑è扉篝飑磲疸犷＇骒狒翦飑┅ㄤ彐躅篝蜷翦舂篝蜷铉趄轫Ж＼羽徙＼葬＼五黛轭濠翦舂ㄤ彐躅屮趄徙舡翦ㄣ镱翦铘祜镳骘翦轭磲疸狎＇篝蜷ㄦ灬趑孱磲疸狎＇珏舡翦泔铘孱舂┅殒ㄡ钿翦戾铉翳翦舂癌泔祆邈翦舂ㄤ彐躅蝈盹鲥轭溴翦舂ㄣ镥蜚戾è滹铄紊泰ㄩ钿屮箦ㄣ镥蜚氨渤吹斗腹ъ轶舂┅祜镳骘汨狎徙翦轭ㄣ镥蜚翦ъ轶舂殒铒ㄦ轭汨狎徙翦轭溴箦舂滹箦赳滹铄冤殒滹铄泔祆邈汨狎徙翦颟篝蜷铉┅ㄤ彐躅黼糸綮翦舂篝蜷蝈盹鲥轭溴篝蜷翦舂┅ㄤ彐躅蝈徜糸綮瀛轭溴糸綮濠戾è轭溴箦ㄣ镥蜚氨渤吹斗腹ъ轶舂铛礅弪Ж┅ㄣ躜蝈铘铛礅弪癌祜镳骘汨狎徙翦轭ㄣ镥蜚篝蜷糸綮濠ъ轶舂躅糸铒ㄦ轭汨狎徙翦轭溴箦舂滹ㄩㄥ聃犰汨狎徙翦＼痱镧瘐箬沲蝌孱舡铛礅弪铛礅弪螬箦翩沲蝌孱舡铛礅弪癌箦翩沲蝌孱舡铛礅弪ǐí沲蝌孱舡铛礅弪卑ㄤ殓轸汨狎汨狎徙翦颟┅┅麒孱沲蝌孱舡铛礅弪癌瘐箬沲蝌孱舡铛礅弪铛礅弪螬蝈鲥蝮铛礅弪螬┅ㄤ彐躅轭沆蹁屦翦舂ㄡ钿戾铉翳翦舂辈篝蜷铉篚怏羼篝蜷翦舂辈轭沆蹁搴孩┅ㄤ彐躅骖犴瀛骝镯轭溴ㄩ钿屮麒孱戾铉翳轭溴癌ㄦ矧磲⒔浇狺ア轭溴ㄡ篌弪紊泰戾è骖犴ㄣ镥蜚ㄦ矧磲紊铂О溥ㄦ轵篝轭溴┅ъ轶舂┅祜镳骘铛轭篚怏羼轭溴暴滹箦赳骖犴ㄡ痧孱骖犴扉篝ㄣ镤瀛汨狎ǐō铛暴ㄣ栳颦泔溴＼岍┅┅┅ㄣ镥蜚骖犴篝蜷铉┅ㄤ彐躅盱蟓麸蝮盱痖钿屮疳珏镳糸镱犰扉篝溴痿暴ㄣ镱è篝蜷铉盱蝈珏蝈痨徙瀛犰⑧盱④苘茑┅è狒镯盱紊泰è篝蜷铉ㄦ轵篝盱┅ㄣ镱è矧篝蜷铉ㄦ轵篝盱稷篝蜷铉ㄦ轵篝盱潇篝蜷铉ㄦ轵篝盱漪┅ㄦ矧磲紊狺ア磲疸狎ㄌ镣履ㄘ盱蟓麸蝮痖钿屮疳珏扉篝溴痿瑭ㄣ滗盱┅┅è篝蜷铉ㄦ轵篝盱滗ㄦ矧磲紊狺ア磲疸狎ㄌ镣履ㄘ盱蟓麸蝮痖钿屮疳珏扉篝溴痿瑭ㄣ滗盱┅┅è篝蜷铉ㄦ轵篝盱痱澧ㄦ矧磲紊ギ泔溴忪镢牒泔眄镱扉箴狺"
                (split #\Newline
                       (format NIL "狺盱蟓麸蝮ㄣ滗盱痖钿屮疳珏扉篝溴痿瑭┅┅è篝蜷铉ㄦ轵篝盱⑩戾è翦ㄦ矧磲紊狺磲疸狎ㄌ镣履ㄘ盱蟓麸蝮痖钿屮疳珏扉篝溴痿瑭ㄣ滗盱┅┅麒孱铒篝蜷铉翦┅ㄩㄣ栳蚪ㄣ栳翦癌＼喋翦ㄦ矧磲紊岐翦舂┅┅è篝蜷铉ㄦ轵篝盱㈤戾è翦ㄦ矧磲紊狺磲疸狎ㄌ镣履ㄘ盱蟓麸蝮痖钿屮疳珏扉篝溴痿瑭ㄣ滗盱┅┅麒孱铒篝蜷铉翦┅ㄩㄣ栳蚪ㄣ栳翦癌＼喋翦ㄦ矧磲紊岐翦舂┅┅è篝蜷铉ㄦ轵篝盱Ⅴ膦ㄦ矧磲紊狺磲疸狎ㄌ镣履ㄘ盱蟓麸蝮痖钿屮疳珏ū扉篝溴痿瑭┅ㄣ滗盱┅┅è篝蜷铉ㄦ轵篝盱㈣并戾舄è翦ㄦ矧磲紊狺ㄦ灬趑孱ㄧ弭翦盱┅┅糸綮黼糸綮翦舂ㄩ钿屮蝈徜糸綮瀛轭溴翦舂┅ㄩㄡ钿轭溴铒ㄥ聃犰轭溴痖钿屮┅戾铉翳轭溴戾铉翳痖钿屮┅ㄥ聃犰篚怏羼轭溴戾铉翳痖钿屮┅痖钿屮┅ㄦ矧磲紊ギ轭沆蹁搴岙蝮簪ㄦ钺礤骝镯轭溴轭溴┅痱镧ㄦ矧磲紊狺狺ア糸綮磲脲篝蜷铉戾铉翳糸綮濠洪铋糸犰屐屙孱ㄣ栳瘴呐姨晌庞戾铉翳轭溴┅┅┅┅è篝蜷铉ㄦ轵篝盱⑨戾è翦篝蜷铉趄轫Ж＼＼羽徙＼葬＼五黛轭濠ㄦ矧磲紊狺ㄦ灬趑孱磲疸狎ㄌ镣履ㄘ盱蟓麸蝮痖钿屮疳珏扉篝溴痿瑭ㄣ滗盱┅┅┅ㄩ戾铉翳翦舂癌ㄩㄩ钽祯溴翦舂翦痱镧戾è扉铍翦紊泰痱镳ㄧ弭轭麸痨弼屐箦泔钿盱㈣蝈姊┅祜镳骘脲栩彐轭痱镳殒篝蜷铉脲㈣蝈姊滹换羽栝铠躞弩梏盱骈戾蟋铒梏麒孱铒箫礤＇ㄌ镣履茅ㄣ栳蚪＼憨栩彐┅箦翩栩彐蝈珏蝈痨徙④梏恝栩彐梏盱┅箦翩ㄧ弭栳箬翦扉铍翎忪疳珏┅栩彐箦翩扉铍翦ㄦ矧磲紊⑧徉撷翦舂┅ㄩ扉铍翦扉铍翦翦舂┅┅┅è篝蜷铉ㄦ轵篝盱㈧棰ㄦ矧磲紊岐" (make-string (max 0 list-depth)
                                                   :initial-element #\Space)
                (flatten (mapcar
                          (LAMBDA (X) (xmls-to-rst X pindex page list-depth))
                          (cdr xml)))))
       ((listp xml) (mapcar (LAMBDA (X) (xmls-to-rst X pindex
                                                     page list-depth))
                            xml))))
    ((listp xml) (mapcar (LAMBDA (X) (xmls-to-rst X pindex page list-depth))
                         xml))))


(defun get-content (page)
  (let ((hr-count 0))
    (loop for tag in page
       if (and (listp tag)
               (stringp (first tag))
               (string= (first tag) "hr"))
       do (incf hr-count)
       if (and (= hr-count 1)
               (not (and (listp tag)
                         (stringp (first tag))
                         (string= (first tag) "hr"))))
       collect tag)))


(defun parse (fname)
  (with-open-file (f fname)
    (let ((text (make-string (file-length f))))
      (read-sequence text f)
      (node-to-xmls (parse-html5-fragment text) NIL))))


(defun parse-page (&optional (fname "Front/Contents.htm"))
  (let ((front-content (parse fname)))
    (make-instance 'spec-page
                   :content front-content)))

(defun read-docs (&optional (path "./"))
  (loop for f in (list-directory (merge-pathnames path "Body"))
     collect (let ((fname (file-namestring f))
                   (page (parse-page f)))
               (list page fname))))


(defun toctree (spec docs)
  (let ((fstr (make-array '(0) :element-type 'base-char
                          :fill-pointer 0 :adjustable t)))
    (with-output-to-string (s fstr)
      ;; Index name
      (let* ((title (mktitle (title spec)))
             (underline (make-string (length title) :initial-element #\*)))
        (format s "a%a%a%%" underline title underline))

      (format s "
.. toctree::
   :maxdepth: 2
   :numbered:

")

      ;; Index contents
      (let ((index (read-title-index (title spec))))
        (loop for (page fname) in docs do
             (let ((page-index (read-title-index (title page))))
               (when (and page-index
                          (= 1 (- (length page-index) (length index)))
                          (equalp (subseq page-index 0 (length index))
                                  index))
                 (format s "   a%" (subseq fname 0 (- (length fname) 4)))))))

      fstr)))


(defun print-docs (docs &optional (path "./"))
  (with-open-file (index (format NIL "a/index.rst" path) :direction :output)
    (format index "Welcome to BlueSpec's documentation!
====================================

Contents:

.. toctree::
   :maxdepth: 2
   :numbered:

")
    (loop for (spec fname) in docs do
         (let ((oname (format NIL "a/a.rst"
                              path
                              (subseq fname 0 (- (length fname) 4))))
               (depth (length (read-title-index (title spec)))))
           ;; (format t "a -> a%" fname oname)
           (with-open-file (f oname :direction :output)
             (if (or (= depth 1)
                     (toctreep spec))
                 (progn (when (= depth 1)
                          (format index "   a%"
                                   (subseq fname 0 (- (length fname) 4))))
                        (format f "a" (strip (toctree spec docs))))
                 (progn (format f "a" (strip (rst spec)))
                        (format f "%%")
                        (loop for key being the hash-keys in (link-table spec)
                           using (hash-value value) do
                             (format f ".. _a: a%"
                                     (regex-replace-all ":" key "\\:")
                                     value)))))))))


(defun reload-docs (docs)
  (loop for (spec fname) in docs
     collect (list (make-instance 'spec-page :content (raw spec))
                   fname)))
